plugins {
    id 'base'
}

repositories {
    mavenCentral()
}

tasks.named('build') {
    dependsOn gradle.includedBuilds*.task(':build')
    dependsOn ':docs-adoc:asciidoctor'
}

tasks.named('clean') {
    dependsOn gradle.includedBuilds*.task(':clean')
}

tasks.named('assemble') {
    dependsOn gradle.includedBuilds*.task(':assemble')
}

tasks.register('sonarqube') {
    dependsOn gradle.includedBuilds*.task(':sonarqube')
}

tasks.register('dollyValidation') {
    description = "Runs the dollyValidation task for all included application builds."
    group = "verification"
    def targetBuilds = gradle.includedBuilds.findAll { includedBuild ->
        def parentDirName = includedBuild.projectDir.parentFile.name
        parentDirName == 'apps' || parentDirName == 'proxies'
    }
    dependsOn targetBuilds*.task(':dollyValidation')
}
