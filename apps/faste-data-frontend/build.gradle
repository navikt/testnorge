plugins {
    id 'java'
    id "org.sonarqube" version "4.4.1.3373"
    id 'org.springframework.boot' version "3.2.1"
    id 'io.spring.dependency-management' version "1.1.4"
    id "jacoco"
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

sonarqube {
    properties {
        property "sonar.coverage.jacoco.xmlReportPaths", "${project.layout.buildDirectory}/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.dynamicAnalysis", "reuseReports"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.language", "java"
        property "sonar.token", System.getenv("SONAR_TOKEN")
        property "sonar.organization", "navikt"
        property "sonar.project.monorepo.enabled", true
        property "sonar.projectKey", "testnav-faste-data-frontend"
        property "sonar.projectName", "testnav-faste-data-frontend"
        property "sonar.sourceEncoding", "UTF-8"
        property 'sonar.sources', 'src/main/java,src/main/js'
        property 'sonar.tests', 'src/test/java'
    }
}

bootJar {
    archiveFileName = "app.jar"
}
dependencyManagement {
    applyMavenExclusions = false
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:2023.0.0'
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

configurations {
    configureEach {
        exclude group: 'ch.qos.logback', module: 'logback-classic'
    }
}

dependencies {
    implementation('no.nav.testnav.libs:security-core') {
        exclude group: 'org.springframework.session', module: 'spring-session-data-redis'
    }

    implementation 'no.nav.testnav.libs:reactive-core'

    implementation('no.nav.testnav.libs:reactive-session-security') {
        exclude group: 'org.springframework.session', module: 'spring-session-data-redis'
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-data-redis'
    }

    implementation 'no.nav.testnav.libs:reactive-frontend'

    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

    implementation 'org.springframework.cloud:spring-cloud-starter-vault-config'
    implementation 'org.springframework.cloud:spring-cloud-starter-gateway'

    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    implementation 'io.micrometer:micrometer-registry-prometheus'
    implementation 'net.logstash.logback:logstash-logback-encoder:7.4'
    implementation 'org.hibernate.validator:hibernate-validator'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.cloud:spring-cloud-contract-wiremock'

    implementation 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
}
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}
