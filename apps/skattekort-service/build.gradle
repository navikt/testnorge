plugins {
    id "dolly-apps"
}

sonarqube {
    properties {
        property "sonar.projectKey", "testnav-skattekort-service"
        property "sonar.projectName", "testnav-skattekort-service"
    }
}

configurations {
    xjc
}

def xjcOutputDir = layout.buildDirectory.dir("generated/sources/xjc")

static def resetOutputDir(Provider outputDirProvider) {
    def outputDir = outputDirProvider.get().asFile
    if (outputDir.exists()) {
        outputDir.deleteDir()
    }
    outputDir.mkdirs()
}

tasks.register("xjcGenerate", JavaExec) {
    def schemaDir = "src/main/resources/schema"
    def outputDirProvider = xjcOutputDir

    inputs.files(fileTree(schemaDir) { include "skattekorttilarbeidsgiver_v3.0.xsd" })
    outputs.dir(outputDirProvider)

    classpath = configurations.xjc
    mainClass = "com.sun.tools.xjc.XJCFacade"

    argumentProviders.add(new CommandLineArgumentProvider() {
        @Override
        Iterable<String> asArguments() {
            return [
                "-encoding", "UTF-8",
                "-extension",
                "-d", outputDirProvider.get().asFile.absolutePath,
                "${schemaDir}/skattekorttilarbeidsgiver_v3.0.xsd"
            ]
        }
    })

    doFirst {
        resetOutputDir(outputDirProvider)
    }
}

sourceSets {
    main {
        java {
            srcDir xjcOutputDir
        }
    }
}

compileJava.dependsOn xjcGenerate

dependencies {
    implementation "no.nav.testnav.libs:data-transfer-objects"
    implementation "no.nav.testnav.libs:reactive-core"
    implementation "no.nav.testnav.libs:reactive-security"
    implementation "no.nav.testnav.libs:security-core"
    implementation "no.nav.testnav.libs:testing"

    implementation "org.springframework.boot:spring-boot-starter-oauth2-client"
    implementation "org.springframework.boot:spring-boot-starter-security"

    implementation "io.swagger.core.v3:swagger-annotations-jakarta:$versions.swagger"
    implementation "jakarta.xml.bind:jakarta.xml.bind-api:$versions.jakartaXmlBindApi"
    implementation "org.apache.httpcomponents.core5:httpcore5:5.3"
    implementation "org.glassfish.jaxb:jaxb-runtime:$versions.jaxb"
    implementation "org.json:json:20231013"
    implementation "org.springdoc:springdoc-openapi-starter-webflux-ui:$versions.springdoc"

    implementation "ma.glasnost.orika:orika-core:$versions.orika"

    testImplementation "io.projectreactor:reactor-test:$versions.reactorTest"

    xjc "org.glassfish.jaxb:jaxb-xjc:4.0.5"
    xjc "com.sun.xml.bind:jaxb-impl:$versions.jaxb"
    xjc "com.sun.istack:istack-commons-runtime:4.2.0"
}