pluginManagement {
    repositories {
        gradlePluginPortal()
        mavenCentral()
        maven {
            url = "https://repository-cdn.liferay.com/nexus/content/groups/public"
        }
    }
}

plugins {
    id "com.gradle.develocity" version "4.0.1"
}

rootProject.name = "testnav"

develocity {
    buildScan {
        termsOfUseUrl = "https://gradle.com/terms-of-service"
        termsOfUseAgree = "yes"
    }
}

def includeAllBuildsFrom(String subdir) {
    file(subdir).eachDir {
        dir ->
            if (!dir.name.startsWith(".") && new File(dir, "build.gradle").exists()) {
                includeBuild dir.path
            }
    }
}

includeAllBuildsFrom("./apps")
includeAllBuildsFrom("./libs")
includeAllBuildsFrom("./mocks")
includeAllBuildsFrom("./plugins")
includeAllBuildsFrom("./proxies")
includeAllBuildsFrom("./xsd")
include 'docs-adoc'
