pluginManagement {
    repositories {
        gradlePluginPortal()
        mavenCentral()
        maven {
            url "https://repository-cdn.liferay.com/nexus/content/groups/public"
        }
    }
}

plugins {
    id "com.gradle.develocity" version "3.17.4"
}

rootProject.name = "testnorge"

develocity {
    buildScan {
        termsOfUseUrl = "https://gradle.com/terms-of-service"
        termsOfUseAgree = "yes"
    }
}

def includeAllBuildsFrom(String dirPath) {
    file(dirPath).eachDir {
        dir ->
            if (!dir.name.startsWith(".")) {
                includeBuild dir.path
            }
    }
}

includeAllBuildsFrom("./apps")
includeAllBuildsFrom("./examples")
includeAllBuildsFrom("./libs")
includeAllBuildsFrom("./mocks")
includeAllBuildsFrom("./plugins")
includeAllBuildsFrom("./proxies")
includeAllBuildsFrom("./xsd")
