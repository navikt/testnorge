plugins {
    id "dolly-sonar"
    id "dolly-versions"

    id "io.spring.dependency-management" // See ../../../build.gradle for version.
    id "java-library"
}

group = "no.nav.testnav.libs"

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencyManagement {
    applyMavenExclusions = false
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:$versions.springBoot"
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:$versions.springCloud"
    }
}

dependencies {
    annotationProcessor "org.projectlombok:lombok"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    implementation "org.projectlombok:lombok"

    testAnnotationProcessor "org.projectlombok:lombok"

    testImplementation "org.springframework.boot:spring-boot-starter-test"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
    maven {
        name = "Confluent"
        url = uri("https://packages.confluent.io/maven/")
    }
    mavenLocal()
}

tasks.test {
    useJUnitPlatform()
    doFirst {
        jvmArgs += '-javaagent:' + configurations.testRuntimeClasspath.find { it.name.contains("byte-buddy-agent") }
        jvmArgs "--add-opens", "java.base/java.lang=ALL-UNNAMED"
    }
}