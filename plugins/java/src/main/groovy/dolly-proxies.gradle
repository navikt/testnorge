plugins {
    id "dolly-sonar"
    id "dolly-versions"
    id "no.nav.dolly.plugins.buildValidation"

    id "idea"
    id "io.spring.dependency-management"
    id "java"
    id "org.springframework.boot"
}

group = "no.nav.testnav.proxies"

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    annotationProcessor "org.projectlombok:lombok"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    compileOnly "org.projectlombok:lombok"

    developmentOnly "org.springframework.boot:spring-boot-devtools"

    implementation "com.google.cloud:spring-cloud-gcp-starter-secretmanager:$versions.gcpSecretManager"
    implementation "net.logstash.logback:logstash-logback-encoder:$versions.logback"
    implementation "org.glassfish.expressly:expressly:$versions.expressly"
    implementation "org.hibernate.validator:hibernate-validator"
    implementation "org.projectlombok:lombok"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-oauth2-resource-server"
    implementation "org.springframework.cloud:spring-cloud-starter-gateway"
    implementation "org.springframework.boot:spring-boot-starter-undertow"
    implementation("org.springframework.boot:spring-boot-starter-webflux") {
        exclude group: "org.springframework.boot", module: "spring-boot-starter-reactor-netty"
    }

    runtimeOnly "io.grpc:grpc-netty:$versions.grpc"
    runtimeOnly "io.micrometer:micrometer-registry-prometheus"

    testAnnotationProcessor "org.projectlombok:lombok"

    testImplementation platform("org.junit:junit-bom:$versions.junit")
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework.cloud:spring-cloud-contract-wiremock"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}

dependencyManagement {
    applyMavenExclusions = false
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:$versions.springBoot"
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:$versions.springCloud"
        mavenBom "org.springframework.session:spring-session-bom:$versions.springSession"
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
    maven {
        name = "GitHub Packages"
        url = uri("https://maven.pkg.github.com/navikt/maven-release")
        credentials {
            username = "token"
            password = System.getenv("NAV_TOKEN")
        }
    }
    maven {
        name = "Confluent"
        url = uri("https://packages.confluent.io/maven/")
    }
    maven {
        name = "Shibboleth"
        url = uri("https://build.shibboleth.net/maven/releases/")
    }
    mavenLocal()
}

tasks.bootJar {
    archiveFileName = "app.jar"
    duplicatesStrategy = DuplicatesStrategy.WARN
}

tasks.test {
    useJUnitPlatform()
    doFirst {
        jvmArgs += '-javaagent:' + configurations.testRuntimeClasspath.find { it.name.contains("byte-buddy-agent") }
        jvmArgs "--add-opens", "java.base/java.lang=ALL-UNNAMED"
    }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}
