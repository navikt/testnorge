plugins {
    id "org.sonarqube" version "4.0.0.2929"
    id 'java-library'
    id 'maven-publish'
    id 'io.spring.dependency-management' version "1.1.0"
    id "jacoco"
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

sonarqube {
    properties {
        property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.dynamicAnalysis", "reuseReports"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.language", "java"
        property "sonar.login", System.getenv("SONAR_TOKEN")
        property "sonar.organization", "navikt"
        property "sonar.project.monorepo.enabled", true
        property "sonar.projectKey", "testnav-reactive-security"
        property "sonar.projectName", "testnav-reactive-security"
        property "sonar.sourceEncoding", "UTF-8"
    }
}


group = 'no.nav.testnav.libs'

repositories {
    mavenCentral()
    mavenLocal()
}
if (project.hasProperty("releaseVersion")) {
    version = releaseVersion
}

java.sourceCompatibility = JavaVersion.VERSION_20

publishing {
    repositories {
        maven {
            name = "github"
            url = uri('https://maven.pkg.github.com/navikt/testnorge')
            credentials(PasswordCredentials) {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
}

dependencyManagement {
    applyMavenExclusions = false
    imports {
        mavenBom 'org.springframework.boot:spring-boot-dependencies:3.0.4'
    }
}

dependencies {

    implementation 'no.nav.testnav.libs:security-core'
    implementation 'com.auth0:java-jwt:4.3.0'

    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    implementation 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'

}