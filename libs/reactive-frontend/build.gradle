import no.nav.testnav.dependencies.gradle.DependenciesPlugin

plugins {
    id 'no.nav.testnav.dependencies'
    id 'java-library'
    id 'maven-publish'
    id 'io.spring.dependency-management' version "1.0.11.RELEASE"
}

group = 'no.nav.testnav.libs'

repositories {
    maven {
        url = uri('https://repo1.maven.org/maven2/')
    }
    mavenLocal()
}

dependencyManagement {
    imports {
        mavenBom  "org.springframework.cloud:spring-cloud-dependencies:${DependenciesPlugin.springCloud}"
    }
}


if (project.hasProperty("releaseVersion")) {
    version = releaseVersion
}

java.sourceCompatibility = JavaVersion.VERSION_17

publishing {
    repositories {
        maven {
            name = "github"
            url = uri('https://maven.pkg.github.com/navikt/testnorge')
            credentials(PasswordCredentials) {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

dependencies {
    implementation 'no.nav.testnav.libs:security-core'

    implementation "org.projectlombok:lombok:${DependenciesPlugin.lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${DependenciesPlugin.lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${DependenciesPlugin.lombokVersion}"

    implementation "org.springframework.boot:spring-boot-starter-oauth2-resource-server:${DependenciesPlugin.springBoot}"
    implementation "org.springframework.boot:spring-boot-starter-webflux:${DependenciesPlugin.springBoot}"

    implementation 'org.springframework.cloud:spring-cloud-starter-vault-config'
    implementation 'org.springframework.cloud:spring-cloud-starter-gateway'

    implementation 'org.hibernate.validator:hibernate-validator'
}