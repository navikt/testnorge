plugins {
    id "dolly-java-conventions"
    id "org.sonarqube" version "5.0.0.4638"
    id 'java-library'
    id 'maven-publish'
    id 'io.spring.dependency-management' version "1.1.5"
    id "jacoco"
}

sonarqube {
    properties {
        property "sonar.dynamicAnalysis", "reuseReports"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.language", "java"
        property "sonar.token", System.getenv("SONAR_TOKEN")
        property "sonar.organization", "navikt"
        property "sonar.project.monorepo.enabled", true
        property "sonar.projectKey", "testnav-reactive-frontend"
        property "sonar.projectName", "testnav-reactive-frontend"
        property "sonar.sourceEncoding", "UTF-8"
    }
}


group = 'no.nav.testnav.libs'

repositories {
    mavenCentral()
    mavenLocal()
}

dependencyManagement {
    applyMavenExclusions = false
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:2023.0.1'
        mavenBom 'org.springframework.boot:spring-boot-dependencies:3.2.1'
    }
}


if (project.hasProperty("releaseVersion")) {
    version = releaseVersion
}

publishing {
    repositories {
        maven {
            name = "github"
            url = uri('https://maven.pkg.github.com/navikt/testnorge')
            credentials(PasswordCredentials) {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
}

dependencies {
    implementation 'no.nav.testnav.libs:security-core'

    implementation 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    implementation 'org.springframework.cloud:spring-cloud-starter-vault-config'
    implementation 'org.springframework.cloud:spring-cloud-starter-gateway'

    implementation 'org.hibernate.validator:hibernate-validator'
}