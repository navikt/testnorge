import no.nav.testnav.dependencies.gradle.DependenciesPlugin

plugins {
    id "org.sonarqube" version "3.3"
    id 'no.nav.testnav.dependencies'
    id 'java-library'
    id 'maven-publish'
}

sonarqube {
    properties {
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.projectKey", "testnav-data-transfer-objects"
        property "sonar.projectName", "testnav-data-transfer-objects"
        property "sonar.organization", "navit"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.project.monorepo.enabled", true
        property "sonar.login", System.getenv("SONAR_TOKEN")
    }
}


group = 'no.nav.testnav.libs'

repositories {
    mavenCentral()
    maven {
        name = "GitHubPackages"
        url = uri('https://maven.pkg.github.com/navikt/maven-release')
        credentials(PasswordCredentials) {
            password System.getenv("NAV_TOKEN")
            username 'token'
        }
    }
    mavenLocal()
}

dependencies {
    implementation "com.fasterxml.jackson.core:jackson-annotations:${DependenciesPlugin.jackson}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${DependenciesPlugin.jackson}"
    implementation "javax.validation:validation-api:${DependenciesPlugin.validationApi}"
    implementation "org.springdoc:springdoc-openapi-common:${DependenciesPlugin.springdocOpenapi}"

    implementation 'no.nav.tjenestespesifikasjoner:nav-altinn-inntektsmelding:1.2019.08.16-13.46-35cbdfd492d4'

    implementation 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.20'
}

if (project.hasProperty("releaseVersion")) {
    version = releaseVersion
}

java.sourceCompatibility = JavaVersion.VERSION_17

publishing {
    repositories {
        maven {
            name = "github"
            url = uri('https://maven.pkg.github.com/navikt/testnorge')
            credentials(PasswordCredentials) {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}