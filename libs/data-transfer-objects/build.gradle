plugins {
    id "org.sonarqube" version "4.0.0.2929"
    id 'java-library'
    id 'maven-publish'
    id 'io.spring.dependency-management' version "1.1.4"
    id "jacoco"
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

sonarqube {
    properties {
        property "sonar.coverage.jacoco.xmlReportPaths", "${project.layout.buildDirectory}/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.dynamicAnalysis", "reuseReports"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.language", "java"
        property "sonar.token", System.getenv("SONAR_TOKEN")
        property "sonar.organization", "navikt"
        property "sonar.project.monorepo.enabled", true
        property "sonar.projectKey", "testnav-data-transfer-objects"
        property "sonar.projectName", "testnav-data-transfer-objects"
        property "sonar.sourceEncoding", "UTF-8"
    }
}

dependencyManagement {
    applyMavenExclusions = false
    imports {
        mavenBom 'org.springframework.boot:spring-boot-dependencies:3.2.1'
    }
}

group = 'no.nav.testnav.libs'

repositories {
    mavenCentral()
    maven {
        name = "GitHubPackages"
        url = uri('https://maven.pkg.github.com/navikt/maven-release')
        credentials(PasswordCredentials) {
            password System.getenv("NAV_TOKEN")
            username 'token'
        }
    }
    mavenLocal()
}

dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-annotations'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml'
    implementation 'io.swagger.core.v3:swagger-annotations-jakarta:2.2.20'
    implementation 'jakarta.validation:jakarta.validation-api'
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api'
    implementation 'javax.xml.bind:jaxb-api:2.3.1'
    implementation 'org.apache.commons:commons-lang3'
    implementation 'org.springframework:spring-web'

    implementation 'no.nav.tjenestespesifikasjoner:nav-altinn-inntektsmelding:1.2019.08.16-13.46-35cbdfd492d4'

    implementation 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
}

if (project.hasProperty("releaseVersion")) {
    version = releaseVersion
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

publishing {
    repositories {
        maven {
            name = "github"
            url = uri('https://maven.pkg.github.com/navikt/testnorge')
            credentials(PasswordCredentials) {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}